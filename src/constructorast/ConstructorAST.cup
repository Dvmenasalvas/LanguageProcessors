package constructorast;

import java_cup.runtime.*;
import alex.AnalizadorLexico;
import alex.UnidadLexica;
import alex.TokenValue;
import errors.GestionErrores;
import ast.E.*;
import ast.I.*;
import ast.T.*;
import ast.AS;

import java.util.List;
import java.util.ArrayList;

scan with {: return getScanner().next_token(); :};
action code {:
   private AS as = new AS();
:};
parser code {: 
   private GestionErrores errores;
   public void syntax_error(Symbol unidadLexica) {
     errores.errorSintactico((UnidadLexica)unidadLexica);
   }
:};
init with {: 
   errores = new GestionErrores();
   AnalizadorLexico alex = (AnalizadorLexico)getScanner();
   alex.fijaGestionErrores(errores);
:};

//1)Terminales y no terminales
//1.1)Terminales
terminal TokenValue OR, AND, IGUALIGUAL, MAYOR, MENOR, MAYORIGUAL, MENORIGUAL, DISTINTO, IGUAL,
MAS, MENOS, POR, DIV, MOD, POT, NOT, COR_A, COR_C,
ENT, IDEN, PAREN_A, PAREN_C, LLAVE_A, LLAVE_C,
TRUE, FALSE, COMA, PUNTOCOMA, 
INT, BOOLEAN, IF, ELSE, RETURN, WHILE, SWITCH, CASE, DEFAULT,
CONST, STRUCT, VOID, PUNTO, PRINT, READ;

//1.2)No terminales
//Tipos
non terminal Tipo Tipo;
non terminal TipoArray TipoArray;

//Expresiones
non terminal E Exp0, Exp1, Exp2, Exp3, Exp4, Exp5, Exp6, Exp7, Exp8, Exp9;

//Instrucciones
non terminal I Inst, InstIf, InstAsig, InstWhile, InstSwitch, InstStruct, InstDeclFun, InstCallVoidFun,
 InstDecl, InstDeclConst, InstPrint;

//Listas
non terminal List<I> Start, ListDecl;
non terminal List<E> ListArg, Dims, ConArg, Arg, ValoresDecl, ListVal, Corchetes1, Corchetes;
non terminal List<TipoArgumento> ListTipoArg, PrimerTipoArg, TipoArgs;
non terminal List<Case> ListaCase;




//2) Inicio
//Expresiones
//S   ::= Exp0:e PUNTOCOMA S:lista
//{:lista.add(0,e); RESULT = lista;:};
//S   ::=
//{:RESULT= new ArrayList<E>();:};
//Instrucciones
Start   ::= Inst:inst Start:lista_inst
{:lista_inst.add(0,inst); RESULT = lista_inst;:};
Start   ::=
{:RESULT= new ArrayList<I>();:};




//3) Gramatica de expresiones (ordenada por precedencia de operadores de menos a más)

//3.1) Operadores logicos
//OR
Exp0 ::= Exp1: op1 OR:or Exp0: op2
   {:RESULT = as.or(op1, op2, or.getFila(), or.getColumna()); :};
Exp0 ::= Exp1: op1
   {:RESULT = op1; :};

//AND
Exp1 ::= Exp2:op1 AND:and Exp1:op2
   {:RESULT=as.and(op1, op2, and.getFila(), and.getColumna()); :};
Exp1 ::= Exp2: op1
   {:RESULT = op1; :};

//3.2) Operadores comparativos
// ==
Exp2 ::= Exp3:op1 IGUALIGUAL:ig Exp3:op2
	{:RESULT = as.igualIgual(op1, op2, ig.getFila(), ig.getColumna()); :};

// >
Exp2 ::= Exp3:op1 MAYOR: ma Exp3:op2
	{:RESULT = as.mayor(op1, op2, ma.getFila(), ma.getColumna()); :};

// <
Exp2 ::= Exp3:op1 MENOR: me Exp3:op2
	{:RESULT = as.menor(op1, op2, me.getFila(), me.getColumna()); :};

// >=
Exp2::= Exp3:op1 MAYORIGUAL: mi Exp3:op2
	{:RESULT = as.mayorIgual(op1, op2, mi.getFila(), mi.getColumna()); :};

// <=
Exp2::= Exp3:op1 MENORIGUAL: mi Exp3:op2
	{:RESULT = as.menorIgual(op1, op2, mi.getFila(), mi.getColumna()); :};

// !=
Exp2 ::= Exp3:op1 DISTINTO: dist Exp3:op2
	{:RESULT = as.distinto(op1, op2, dist.getFila(), dist.getColumna()); :};

Exp2 ::= Exp3: op
   {:RESULT = op; :};

//3.3) Operadores aritmeticos

//3.3.1) Suma y resta
//+
Exp3 ::= Exp3:op1 MAS:mas Exp4:op2
   {:RESULT=as.suma(op1, op2, mas.getFila(), mas.getColumna()); :};

//-
Exp3 ::= Exp3:op1 MENOS:menos Exp4:op2
   {:RESULT=as.resta(op1, op2, menos.getFila(), menos.getColumna()); :};

Exp3 ::= Exp4:op1
   {:RESULT=op1; :};

//3.3.2) Multiplicacion, division y modulo
//*
Exp4 ::= Exp4:op1 POR:por Exp5:op2
   {:RESULT=as.mul(op1, op2, por.getFila(), por.getColumna()); :};

// /
Exp4 ::= Exp4:op1 DIV:div Exp5:op2
   {:RESULT=as.div(op1, op2, div.getFila(), div.getColumna()); :};

//%
Exp4 ::= Exp4 :op1 MOD:mod Exp5:op2
   {:RESULT=as.mod(op1, op2, mod.getFila(), mod.getColumna()); :};
Exp4 ::= Exp5:e1
{:RESULT=e1;:};


//3.3.3) Potencia
Exp5 ::= Exp5:e1 POT:pot Exp6:e2
{:RESULT=as.pot(e1, e2, pot.getFila(), pot.getColumna());:};
Exp5 ::= Exp6:e1
{:RESULT=e1;:};


//3.4) Negación
Exp6 ::= NOT:not Exp6:e1
    {:RESULT=as.not(e1, not.getFila(), not.getColumna()); :};
Exp6 ::= Exp7:e1
    {:RESULT=e1;:};


//3.5) Operador Punto
Exp7 ::= IDEN:id PUNTO:pun IDEN:id2
    {:RESULT= as.accederStruct(as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), as.iden(id2.getLexema(), null, id2.getFila(), id2.getColumna()), pun.getFila(), pun.getColumna());:};


//3.6) Operador Corchete
Exp7 ::= IDEN: id Corchetes1:cor
    {:RESULT = as.iden(id.getLexema(),cor, id.getFila(), id.getColumna());:};
Corchetes1 ::= COR_A:ca Exp0:exp COR_C Corchetes:cor
    {:cor.add(0, exp); RESULT=cor;:};
Corchetes ::= COR_A Exp0:exp COR_C Corchetes:cor
    {:cor.add(0, exp); RESULT=cor;:};
Corchetes ::=
    {:RESULT=new ArrayList<E>();:};


Exp7 ::= Exp8: exp
    {:RESULT = exp;:};

//3.5) Terminales
Exp8  ::= ENT:e1
{:RESULT=as.ent(e1.getLexema(), e1.getFila(), e1.getColumna());:};
//Falta identificadores con dimension
Exp8  ::= IDEN:e1
{:RESULT=as.iden(e1.getLexema(), null, e1.getFila(), e1.getColumna());:};
Exp8  ::= PAREN_A Exp0:e1 PAREN_C
{:RESULT=e1;:};
Exp8  ::= TRUE:tru
{:RESULT=as.verdadero(tru.getFila(), tru.getColumna());:};
Exp8  ::= FALSE:fal
{:RESULT=as.falso(fal.getFila(), fal.getColumna());:};
Exp8 ::= Exp9:op1
    {:RESULT = op1;:};

//3.6) Llamadas a la función como expresión
Exp9  ::= IDEN:id PAREN_A ListArg:args PAREN_C:pc
    {:RESULT = as.llamadaFuncion(as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), args, id.getFila(), id.getColumna());:};


//4) Tipos

//4.1) Tipos unitarios
Tipo ::= INT: in
    {:RESULT = as.tipoInt(in.getFila(), in.getColumna());:};
Tipo ::= BOOLEAN: boo
    {:RESULT = as.tipoBoolean(boo.getFila(), boo.getColumna());:};
Tipo ::= STRUCT IDEN:stu
{:RESULT = as.tipoStruct(as.iden(stu.getLexema(), null, stu.getFila(), stu.getColumna()), stu.getFila(), stu.getColumna());:};

//4.2)Tipo Array
TipoArray ::= Tipo:tb Dims:dims
{:

RESULT =  as.tipoArray(tb, dims,tb.getFila(), tb.getColumna());:};

Dims ::=
{:RESULT = null;:};
Dims ::= COR_A:ca Exp0:dim COR_C:cc Dims:dims
{:
List<E> result = new ArrayList<>();
result.add(0,dim);
if (dims != null) {
    dims.add(0,dim);
    result = dims;
}
RESULT = result;
:};

//4) Gramatica de instrucciones

//4.1) Instrucciones posibles
Inst ::= InstIf:op1
{:RESULT = op1;:};
Inst ::= InstAsig:op1
{:RESULT = op1;:};
Inst ::= InstSwitch: inst1
    {:RESULT = inst1; :};
Inst ::= InstWhile:op1
{:RESULT = op1;:};
Inst ::= InstDeclFun:op
{:RESULT = op;:};
Inst ::= InstCallVoidFun:op
{:RESULT = op;:};
Inst ::= InstDecl: ins
    {:RESULT = ins;:};
Inst ::= InstDeclConst: ins
    {:RESULT = ins;:};
Inst ::= InstStruct:stu
{:RESULT = stu;:};
Inst ::= InstPrint:ins
    {:RESULT = ins;:};

//4.2) Instruccion asignacion
InstAsig ::= Exp0:id IGUAL:igual Exp0:valor PUNTOCOMA
{:RESULT = as.instAsignacion(id, valor, igual.getFila(), igual.getColumna());:};

InstAsig ::= error PUNTOCOMA
    {:System.err.println("Error en la asignación");:};


//4.3) Instruccion If
InstIf ::= IF:iff PAREN_A Exp0:condicion PAREN_C LLAVE_A Start:cuerpo LLAVE_C
{:RESULT = as.instIf(condicion, cuerpo, null, iff.getFila(), iff.getColumna());:};
InstIf ::= IF:iff PAREN_A Exp0:condicion PAREN_C LLAVE_A Start:cuerpo_if LLAVE_C ELSE LLAVE_A Start:cuerpo_else LLAVE_C
{:RESULT = as.instIf(condicion, cuerpo_if, cuerpo_else, iff.getFila(), iff.getColumna());:};

//Errores
InstIf ::= IF error LLAVE_C
{:System.err.println("Error en la condicion del if");:};
InstIf ::= IF Exp0 error LLAVE_C
{:System.err.println("Error en el cuerpo del if o del else");:};

//4.4) Instrucción while
InstWhile ::= WHILE:w PAREN_A Exp0:cond PAREN_C LLAVE_A Start:cuerpo LLAVE_C
{:RESULT = as.instWhile(cond, cuerpo, w.getFila(), w.getColumna());:};

//Errores
InstWhile ::= WHILE error LLAVE_C
{:System.err.println("Error en la condicion del while");:};
InstWhile ::= WHILE Exp0 error LLAVE_C
{:System.err.println("Error en el cuerpo del while");:};


//4.5) Instrucción switch
InstSwitch ::= SWITCH:swi PAREN_A Exp0: exp PAREN_C LLAVE_A ListaCase:lista_case LLAVE_C
    {:RESULT = as.instSwitch(exp, lista_case, swi.getFila(), swi.getColumna());:};

ListaCase::= CASE:cas Exp0:var LLAVE_A Start:cuerpo LLAVE_C ListaCase:lista_case
    {:lista_case.add(0, as.createCase(var, cuerpo, cas.getFila(), cas.getColumna()));
           RESULT = lista_case; :};

//Default
ListaCase::= DEFAULT: def LLAVE_A Start: cuerpo LLAVE_C
     {: List<Case> lista_case = new ArrayList<Case>();
    	 	lista_case.add(0, as.createCase(as.iden("default", null,def.getFila(), def.getColumna()), cuerpo, def.getFila(), def.getColumna()));
    	 	RESULT = lista_case; :};



//Errores
InstSwitch ::= SWITCH error LLAVE_C
{:System.err.println("Error en la eleccion de la variable del switch");:};
ListaCase ::= CASE error LLAVE_C ListaCase
{:System.err.println("Error en la expresion del case del switch");:};
ListaCase ::= CASE Exp0 error LLAVE_C
{:System.err.println("Error en el cuerpo del case del switch");:};
ListaCase ::= DEFAULT error LLAVE_C
{:System.err.println("Error en el cuerpo del default del switch");:};



// 4.6) Instrucción declaración
//4.6.1) Declaracion sin inicializar
InstDecl ::= TipoArray:tip IDEN:id PUNTOCOMA
    {:RESULT = as.instDecl(tip, as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), null, false, tip.getFila(), tip.getColumna()); :};

//4.6.2) Declaración con valores iniciales
InstDecl ::= TipoArray:tip IDEN:id IGUAL ValoresDecl:vd PUNTOCOMA
    {:RESULT = as.instDecl(tip, as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), vd, false, tip.getFila(), tip.getColumna()); :};

//4.6.3) Declaración constantes inicializadas obligatoriamente
InstDeclConst ::= CONST TipoArray:tip IDEN:id IGUAL ValoresDecl:vd PUNTOCOMA
    {:RESULT = as.instDecl(tip, as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), vd, true,  tip.getFila(), tip.getColumna()); :};

//Errores
InstDecl ::= TipoArray error PUNTOCOMA
{:System.err.println("Error en la declaracion. Identificador erroneo");:};
InstDecl ::= TipoArray IDEN error PUNTOCOMA
{:System.err.println("Error la declaracion. Valor inicial erroneo");:};
InstDeclConst ::= CONST TipoArray error PUNTOCOMA
{:System.err.println("Error en la declaracion. Identificador erroneo");:};
InstDeclConst ::= CONST TipoArray IDEN error PUNTOCOMA
{:System.err.println("Error en la declaracion. Valor inicial erroneo");:};

//Solo soporta una dimension
ValoresDecl ::= Exp0:val
{: List<E> list = new ArrayList<E>(); list.add(0, val); RESULT = list;:};
ValoresDecl   ::= LLAVE_A LLAVE_C
{: List<E> list = new ArrayList<E>(); RESULT = list;:};
ValoresDecl   ::= LLAVE_A ListVal:list LLAVE_C
{:RESULT = list;:};

ListVal  ::= Exp0:val
{:List<E> list = new ArrayList<E>(); list.add(0, val); RESULT = list;:};
ListVal  ::= Exp0:val COMA ListVal:list
{:list.add(0, val); RESULT = list;:};


//4.7) Instruccion struct
InstStruct ::= STRUCT:struct IDEN:nombre LLAVE_A ListDecl:lista LLAVE_C PUNTOCOMA
{:RESULT = as.instStruct(as.iden(nombre.getLexema(), null, nombre.getFila(), nombre.getColumna()), lista, struct.getFila(), struct.getColumna());:};

InstStruct ::= STRUCT error PUNTOCOMA
{:System.err.println("Error en la declaracion del struct. Campo erroneo.\n");:};
InstStruct ::= STRUCT LLAVE_A ListDecl LLAVE_C error PUNTOCOMA
{:System.err.println("Error en la declaracion del struct. Identificador erroneo.\n");:};

ListDecl ::= InstDecl:decl ListDecl:lista
{:lista.add(0, decl); RESULT = lista;:};
ListDecl ::=
{:RESULT = new ArrayList<I>();:};


//4.8) Instrucción declaración función
InstDeclFun ::= TipoArray:tipo IDEN:id PAREN_A ListTipoArg:arg PAREN_C LLAVE_A Start:cuerpo RETURN Exp0:ret PUNTOCOMA LLAVE_C
    {:RESULT = as.instDeclFun(tipo, as.iden(id.getLexema(), null, id.getFila(), id.getColumna()),arg, cuerpo, ret, id.getFila(), id.getColumna());:};
InstDeclFun ::= VOID IDEN:id PAREN_A ListTipoArg:arg PAREN_C LLAVE_A Start:cuerpo LLAVE_C
    {:RESULT = as.instDeclFun(null, as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), arg, cuerpo, null, id.getFila(), id.getColumna());:};


ListTipoArg ::= PrimerTipoArg:a
{:RESULT=a;:};
ListTipoArg ::=
{:RESULT=new ArrayList<TipoArgumento>();:};

PrimerTipoArg ::= TipoArray:tipo IDEN:nombre  TipoArgs:otrosArgs
{:
    otrosArgs.add(0, new TipoArgumento(tipo, as.iden(nombre.getLexema(), null, nombre.getFila(), nombre.getColumna())));
    RESULT=otrosArgs;
:};

TipoArgs ::= COMA TipoArray:tipo IDEN:nombre TipoArgs:otrosArgs
{:
  otrosArgs.add(0, new TipoArgumento(tipo, as.iden(nombre.getLexema(), null, nombre.getFila(), nombre.getColumna())));
  RESULT=otrosArgs;
:};

TipoArgs ::=
{:RESULT=new ArrayList<TipoArgumento>();:};



//Errores
InstDeclFun ::= TipoArray error LLAVE_C
    {:System.err.println("Error en la declaracion de la funcion. Identificador erroneo\n");:};
InstDeclFun ::= TipoArray IDEN error LLAVE_C
    {:System.err.println("Error en la declaracion de la funcion. Parametros erroneos\n");:};
InstDeclFun ::= TipoArray IDEN PAREN_A ListTipoArg PAREN_C error LLAVE_C
    {:System.err.println("Error en la declaracion de la funcion. Cuerpo erroneo\n");:};



//4.9) Instrucción llamada a procedimiento
InstCallVoidFun ::= IDEN:id PAREN_A ListArg:args PAREN_C PUNTOCOMA
    {:RESULT = as.instCallVoidFun(as.iden(id.getLexema(), null, id.getFila(), id.getColumna()), args, id.getFila(), id.getColumna());:};

//Lista de argumentos para las llamadas a funciones
//Sin argumentos
ListArg ::=
    {:RESULT=new ArrayList<E>();:};

//Con argumentos
ListArg ::= ConArg:arg
    {:RESULT=arg;:};
ConArg  ::= Exp0:exp Arg:ar
    {:ar.add(0, exp); RESULT=ar;:};
Arg      ::= COMA Exp0:exp Arg:ar
    {:ar.add(0, exp); RESULT=ar;:};
Arg		 ::=
    {:RESULT=new ArrayList<E>();:};

// 4.10) Instruccion Print
InstPrint ::= PRINT:pr PAREN_A Exp0:exp PAREN_C PUNTOCOMA
  {:RESULT = as.instPrint(exp, pr.getFila(), pr.getColumna());:};


